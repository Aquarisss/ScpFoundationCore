apply plugin: 'com.android.application'
//here is script to generate version code from version name
//which is set in gradle.properties as value of version var
//which must be in format major, minor, patch, where patch goes from 0 to 99
apply from: '../versioning.gradle'

apply from: '../marketSkusCreator.gradle'
apply from: '../apiKeysCreator.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "ru.dante.scpfoundation"
        minSdkVersion 17
        targetSdkVersion 26

        // Enabling multidex support.
        multiDexEnabled true
    }

    signingConfigs {
        release {
            // Load keystore
            if (rootProject.file('keystore.properties').exists()) {
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (rootProject.file('keystore.properties').exists()) {
                signingConfig signingConfigs.release
            }
        }

//        applicationVariants.all { variant ->
//            logger.debug('variant: ' + variant)
//
//            fillArrays(variant, false)
//            fillApiArrays(variant, false)
//
//            variant.mergedFlavor.versionCode = buildVersionCodeFromVariant(variant)
//            variant.mergedFlavor.versionName = getVersionValueAsString(variant)
//        }
    }

    flavorDimensions "lang", "mode"

    productFlavors {
        prod {
            dimension "mode"

            manifestPlaceholders = [myDebuggable: false]
        }

        dev {
            dimension "mode"

            manifestPlaceholders = [myDebuggable: true]
        }

        ru {
            dimension "lang"
            matchingFallbacks = ['debug', 'release']
        }

        en {
            dimension "lang"

            applicationIdSuffix ".eng"
            matchingFallbacks = ['debug', 'release']
        }

        pl {
            dimension "lang"

            applicationIdSuffix ".pl"
            matchingFallbacks = ['debug', 'release']
        }

        de {
            dimension "lang"

            applicationIdSuffix ".de"
            matchingFallbacks = ['debug', 'release']
        }

        fr {
            dimension "lang"

            applicationIdSuffix ".fr"
            matchingFallbacks = ['debug', 'release']
        }

        es {
            dimension "lang"

            applicationIdSuffix ".es"
            matchingFallbacks = ['debug', 'release']
        }

        italy {
            dimension "lang"

            applicationIdSuffix ".it"
            matchingFallbacks = ['debug', 'release']
        }

        applicationVariants.all { variant ->
            println('variantApp: ' + variant.getName())

            fillArrays(variant, false)
            fillApiArrays(variant, false)

            def versionCode = buildVersionCodeFromVariant(variant)
            def versionName = getVersionValueAsString(variant)
            println('versionCode: ' + versionCode)
            println('versionName: ' + versionName)

            println('variant.mergedFlavor.versionCode: ' + variant.mergedFlavor.versionCode)
            println('variant.mergedFlavor.versionName: ' + variant.mergedFlavor.versionName)

            variant.mergedFlavor.versionCode = versionCode
            variant.mergedFlavor.versionName = versionName

            println('variant.mergedFlavor.versionCode: ' + variant.mergedFlavor.versionCode)
            println('variant.mergedFlavor.versionName: ' + variant.mergedFlavor.versionName)

            //need this as in new gradle plugin there broken functionality of iterating variant during build
            //so we need to set version so
            //see https://developer.android.com/studio/build/gradle-plugin-3-0-0-migration.html#variant_output
            variant.outputs.all { output ->
                output.versionCodeOverride = versionCode
                output.versionNameOverride = versionName
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    //dev
    ruDevReleaseCompile
    enDevReleaseCompile
    plDevReleaseCompile
    deDevReleaseCompile
    frDevReleaseCompile
    esDevReleaseCompile
    italyDevReleaseCompile
    //prod
    ruProdReleaseCompile
    enProdReleaseCompile
    plProdReleaseCompile
    deProdReleaseCompile
    frProdReleaseCompile
    esProdReleaseCompile
    italyProdReleaseCompile
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //dagger2
    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    //core module
//    compile project(path: ':core', configuration: 'devRelease')
    implementation project(':core')

//    //to use lib via gradle add this lines:
//    //leakcanary (we need it here too according to https://github.com/square/leakcanary/issues/514#issuecomment-318203708)
//    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
//    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
//    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
//    //add core itself at last for each configuration
//    ruDevReleaseCompile(group: 'ru.kuchanov.scp', name: 'core', version: '1.0.0', ext: 'aar', classifier: 'devRelease'){transitive = true}
//    //or
//    ruDevReleaseCompile('ru.kuchanov.scp:core:1.0.1:devRelease@aar') { transitive = true }
}

apply plugin: 'com.google.gms.google-services'